plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

allprojects {
    group = 'farm2pot'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
}

ext {
    redisOmVersion = '0.9.10'
    querydslVersion = '5.1.0'
    jwtVersion = '0.12.6'
    mapstructVersion = '1.6.3'
    jacksonVersion = '2.19.1'
    caffeineVersion = '3.2.1'
    p6spyVersion = '1.11.0'
    springdocVersion = '2.8.9'
    ulidVersion = '5.2.3'

    annotationVersion = '3.0.0'
    persistenceVersion = '3.2.0'
    mybatisVersion = '3.5.15'
    commonsCollectionsVersion = '4.5.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = rootProject.group
    version = rootProject.version

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.3"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.3"
        }
    }

    ext {
        isMacOs = System.getProperty('os.name').toLowerCase().contains('mac')
    }

    // common Ï†úÏô∏ÌïòÍ≥† ÎÇòÎ®∏ÏßÄÏóê Í≥µÌÜµ ÏùòÏ°¥ÏÑ± Ï∂îÍ∞Ä
    if (project.name != 'common') {
        dependencies {
            implementation project(':common')  // Î™®Îì† appsÏóê common Ï∂îÍ∞Ä
        }
    }

    dependencies {
        //implementation 'org.springframework.boot:spring-boot-starter-web'

        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:3.5.3'
        runtimeOnly 'mysql:mysql-connector-java:8.0.33'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation "org.apache.commons:commons-collections4:$commonsCollectionsVersion"

        /** Spring-Doc **/
        // implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: "$springdocVersion"

        /** AOP **/
        implementation 'org.springframework.boot:spring-boot-starter-aop'

        /** JWT **/
        implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: "$jwtVersion"
        runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: "$jwtVersion"
        runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: "$jwtVersion"

        /** Spring-Security-Crypto **/
        implementation 'org.springframework.security:spring-security-crypto'

        /** Apache HttpClient for RestTemplate connection pooling **/
        implementation 'org.apache.httpcomponents.client5:httpclient5'

        /** REDIS **/
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation 'redis.clients:jedis:5.1.0'

        /** Reflections **/
        implementation 'org.reflections:reflections:0.10.2'

        /** Map Struct **/
        implementation "org.mapstruct:mapstruct:${mapstructVersion}"
        annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
        annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"

        /** Jackson **/
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jacksonVersion"

        /** Caffeine Cache **/
        implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: "$caffeineVersion"

        /** JPA **/
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        /** P6Spy **/
        implementation "com.github.gavlyukovskiy:p6spy-spring-boot-starter:$p6spyVersion"

        // ULID
        implementation group: 'com.github.f4b6a3', name: 'ulid-creator', version: "$ulidVersion"

        // UUID
        implementation "com.fasterxml.uuid:java-uuid-generator:5.1.0"

        /** QueryDSL Implementation **/
        implementation "com.querydsl:querydsl-jpa:$querydslVersion:jakarta"
        implementation "com.querydsl:querydsl-core:$querydslVersion"
        annotationProcessor "com.querydsl:querydsl-apt:$querydslVersion:jakarta"
        annotationProcessor "jakarta.annotation:jakarta.annotation-api:$annotationVersion"
        annotationProcessor "jakarta.persistence:jakarta.persistence-api:$persistenceVersion"

        // Í≥µÌÜµÍ¥ÄÎ†® ÌÖåÏä§Ìä∏ ÏùòÏ°¥ÏÑ±
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.4'
        testImplementation 'org.springframework.security:spring-security-test'

        // JUnit Jupiter APIÏôÄ EngineÏùÑ ÌÖåÏä§Ìä∏ ÏùòÏ°¥ÏÑ±ÏúºÎ°ú Ï∂îÍ∞Ä
        testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.0-M1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.0-M1'

        // Jsoup(for Html cleaner)
        implementation group: 'org.jsoup', name: 'jsoup', version: '1.21.1'

        implementation 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.apache.commons:commons-text:1.13.1'

        /** ÎßàÏù¥Î∞îÌã∞Ïä§ **/
        implementation "org.mybatis:mybatis:$mybatisVersion"

        /** **/
        implementation "org.mapstruct:mapstruct:${mapstructVersion}"
        annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

        if (project.ext.isMacOs) {
            runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.2.2.Final:osx-aarch_64' // MacOS Silicon ÎùºÏù¥Î∏åÎü¨Î¶¨ ÎàÑÎùΩ Î¨∏Ï†ú
        }
    }

    // Í≥µÌÜµ Ïª¥ÌååÏùº ÏÑ§Ï†ï
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.release.set(21)
        options.compilerArgs += ["-Xlint:deprecation", "-parameters"]
    }
}


/* ======================
   üß± CORE MODULE
====================== */
project(":apps:core") {
    apply plugin: 'org.springframework.boot'

    bootJar {
        archiveBaseName.set('core')
        archiveVersion.set(version as String)
        archiveClassifier.set('')
    }

    dependencies {
        // HTTP ÏÉÅÌÉú, MediaType Îì± ÏµúÏÜå Web ÏùòÏ°¥ÏÑ±
        implementation 'org.springframework.boot:spring-boot-starter-web'

        // Validation
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }

    springBoot {
        mainClass = 'com.farm2pot.Main'
    }
}

/* ======================
// üë§ USER MODULE (MVC)
====================== */
project(":apps:user") {
    apply plugin: 'org.springframework.boot'
    bootJar {
        archiveBaseName.set('user')
        archiveVersion.set(version as String)
        archiveClassifier.set('')
    }
    dependencies {
        implementation project(':apps:core')

        implementation 'org.springframework.boot:spring-boot-starter-web'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-security'
    }
    springBoot {
        mainClass = 'com.farm2pot.UserApplication'
    }
}

/* ======================
// üß≠ ADMIN MODULE (MVC)
====================== */
project(":apps:admin") {
    apply plugin: 'org.springframework.boot'
    bootJar {
        archiveBaseName.set('admin')
        archiveVersion.set(version as String)
        archiveClassifier.set('')
    }
    dependencies {
        implementation project(':apps:core')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }
    springBoot {
        mainClass = 'com.farm2pot.AdminApplication'
    }
}

/* ======================
// üì¶ SUBSCRIPTION MODULE (MVC)
====================== */
project(":apps:subscription") {
    apply plugin: 'org.springframework.boot'
    bootJar {
        archiveBaseName.set('subscription')
        archiveVersion.set(version as String)
        archiveClassifier.set('')
    }
    dependencies {
        implementation project(':apps:core')

        implementation 'org.springframework.boot:spring-boot-starter-web'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }
    springBoot {
        mainClass = 'com.farm2pot.SubscriptionApplication'
    }
}

/* ======================
// üåê GATEWAY MODULE (WebFlux)
====================== */
project(":apps:gateway") {
    apply plugin: 'org.springframework.boot'
    bootJar {
        archiveBaseName.set('gateway')
        archiveVersion.set(version as String)
        archiveClassifier.set('')
    }

    dependencies {
        implementation project(':apps:core')

        // WebFlux + Gateway
        implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    }

    // GatewayÎäî ÎùºÏö∞ÌåÖÎßå ÌïòÎØÄÎ°ú JPA Í¥ÄÎ†® ÏùòÏ°¥ÏÑ± Ï†úÏô∏
    configurations {
        implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
        implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-jpa'
        implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-data-redis'
        implementation.exclude group: 'mysql', module: 'mysql-connector-java'
        implementation.exclude group: 'org.mariadb.jdbc', module: 'mariadb-java-client'
        implementation.exclude group: 'com.querydsl'
        implementation.exclude group: 'com.github.gavlyukovskiy', module: 'p6spy-spring-boot-starter'
    }

    springBoot {
        mainClass = 'com.farm2pot.GatewayApplication'
    }
}